.BRA {
.KET }

RESIDENT C:Execute PURE

; Setup paths (must end with /)
SET PathGames "DATA:Games/"
SET PathDemos "DATA:Demos/"
SET PathDeleteGames "Delete/Games/"
SET PathDeleteDemos "Delete/Demos/"
SET PathExtractECSGames "ExtractECS/Games/"
SET PathExtractECSDemos "ExtractECS/Demos/"
SET PathExtractAGAGames "ExtractAGA/Games/"
SET PathExtractAGADemos "ExtractAGA/Demos/"

ASK "Does this Amiga have the AGA chipset?"
IF WARN
	SET ProcessAGA 1
	SET PathGameNames "NamesAGA/Games/"
	SET PathDemoNames "NamesAGA/Demos/"
ELSE
	SET ProcessAGA 0
	SET PathGameNames "NamesECS/Games/"
	SET PathDemoNames "NamesECS/Demos/"
ENDIF

ASK "[1/3] Extract new/changed and delete old slaves?"
IF WARN
	; Delete
	IF EXISTS $PathDeleteGames
		;ECHO "Deleting old or changed from $PathGames..."
		LIST $PathDeleteGames LFORMAT="EXECUTE helper_delete $PathGames%n" >RAM:script
		EXECUTE RAM:script
	ENDIF
	IF EXISTS $PathDeleteDemos
		;ECHO "Deleting old or changed from $PathDemos..."
		LIST $PathDeleteDemos LFORMAT="EXECUTE helper_delete $PathDemos%n" >RAM:script
		EXECUTE RAM:script
	ENDIF

	; Extract ECS
	;ECHO "Extracting $PathExtractECSGames to $PathGames..."
	LIST $PathExtractECSGames LFORMAT="EXECUTE helper_extract %n %l %p $PathGames" >RAM:script
	EXECUTE RAM:script
	;ECHO "Extracting $PathExtractECSDemos to $PathDemos..."
	LIST $PathExtractECSDemos LFORMAT="EXECUTE helper_extract %n %l %p $PathDemos" >RAM:script
	EXECUTE RAM:script

	; Extract AGA
	IF $ProcessAGA EQ 1
		;ECHO "Extracting $PathExtractAGAGames to $PathGames..."
		LIST $PathExtractAGAGames LFORMAT="EXECUTE helper_extract %n %l %p $PathGames" >RAM:script
		EXECUTE RAM:script
		;ECHO "Extracting $PathExtractAGADemos to $PathDemos..."
		LIST $PathExtractAGADemos LFORMAT="EXECUTE helper_extract %n %l %p $PathDemos" >RAM:script
		EXECUTE RAM:script
	ENDIF
ENDIF

; Delete unreferenced
ASK "[2/3] Scan for unreferenced slaves and delete them? (slow)"
IF WARN
	IF EXISTS $PathGameNames
		ECHO "Deleting from $PathGames if not existing in $PathGameNames..."
		LIST $PathGames dirs LFORMAT="IF NOT EXISTS $PathGameNames%n *N EXECUTE helper_delete $PathGames%n *N ENDIF" >RAM:script
		EXECUTE RAM:script
	ENDIF
	IF EXISTS $PathDemoNames
		ECHO "Deleting from $PathDemos if not existing in $PathDemoNames..."
		LIST $PathDemos dirs LFORMAT="IF NOT EXISTS $PathDemoNames%n *N EXECUTE helper_delete $PathDemos%n *N ENDIF" >RAM:script
		EXECUTE RAM:script
	ENDIF
ENDIF

; Search for missing
ASK "[3/3] Scan for missing slaves and notify? (slow)"
IF WARN
	IF EXISTS $PathGameNames
		ECHO "Scanning for missing slaves in $PathGames..."
		LIST $PathGameNames LFORMAT="IF NOT EXISTS $PathGames%n *N ECHO - Missing game %n *N ENDIF" >RAM:script
		EXECUTE RAM:script
	ENDIF
	IF EXISTS $PathDemoNames
		ECHO "Scanning for missing slaves in $PathDemos..."
		LIST $PathDemoNames LFORMAT="IF NOT EXISTS $PathDemos%n *N ECHO - Missing demo %n *N ENDIF" >RAM:script
		EXECUTE RAM:script
	ENDIF
ENDIF

DELETE RAM:script QUIET >NIL:

ECHO ""
ECHO "All done!"